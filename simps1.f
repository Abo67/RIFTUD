      REAL FUNCTION SIMPS1 (X1,X2,FUNC,KSIG)
      IMPLICIT NONE
C  THIS ROUTINE INTEGRATES FUNC(X) FROM X1 TO X2 USING A MULTIPLE
C  INTERVAL SIMPSOWS RULE TECHNIQUE.
      REAL X1,X2
      INTEGER KSIG
      LOGICAL SPILL
      DOUBLE PRECISION ANS,Q
      REAL V,H,A,B,C,P,E
      DIMENSION V(200),H(200),A(200),B(200),C(200),P(200),E(200) 
      REAL TWO,THREE,FOUR,THIRTY
      DATA TWO,THREE,FOUR,THIRTY/2.0,3.0,4.0,30.0/
      REAL T
      INTEGER NMAX,NSIG
      DATA T,NMAX,NSIG/3.0E-5,200,1/
      REAL QQ,TT,TEST,FRAC
      INTEGER K,N,NN,I
      REAL SIMPX1
      REAL FUNC
      EXTERNAL FUNC
C  INITIALIZE FIRST ELEMENTS OF ARRAYS.
      V(1)= X1
      H(1)= (X2 - V(1))/TWO
      A(1)= FUNC ( V )
      B(1)= FUNC ( V(1)+H(1) )
      C(1)= FUNC ( X2 )
      P(1)= H(1)*(A(1) + FOUR*B(1) + C(1))
      E(1)= P(1)
      ANS= P(1)
      N=1
      FRAC=T
      SPILL=.FALSE.
1     TEST=ABS(FRAC*ANS)
      K=N
      DO 3 I=1,K
C  TEST MAGNITUDE OF 4TH ORDER ERROR IN THIS INTERVAL.
      IF (ABS(E(I)).LE.TEST) GO TO 3
      IF (N.LT.NMAX) GO TO 2
C GO TO FINISH IF STORAGE IS FILLED UP.
      SPILL=.TRUE.
      KSIG=KSIG+NSIG
      GO TO 4
C SUBDIVIDE INTERVAL AGAIN TO REDUCE 4TH ORDER ERROR.
2     N=N+1
      V(N)=V(I)+H(I)
      H(N)=H(I)/TWO
      A(N)=B(I)
      B(N)=FUNC(V(N)+H(N))
      C(N)=C(I) 
      P(N)=H(N)*(A(N)+FOUR*B(N)+C(N))
      H(I)=H(N)
      B(I)=FUNC(V(I)+H(I))
      C(I)=A(N) 
      Q=P(I)
      P(I)=H(I)*(A(I)+FOUR*B(I)+C(I))
      Q=P(I)+P(N)-Q
      ANS=ANS+Q 
      E(I)=Q
      E(N)=Q
3     CONTINUE
C TEST ALL INTERVALS AGAIN IF ANY WERE SUBDIVIDED THE LAST TIME. 
      IF (N.GT.K) GO TO 1
4     Q=0.0
      DO 5 I=1,N
         Q=Q+E(I)
5     CONTINUE
C TIGHTEN ERROR LIMIT IF TOTAL ACCUMULATED ERROR TOO LARGE.
      IF (ABS(Q/T).LE.ABS(ANS).OR.SPILL) GO TO 6
      FRAC=FRAC/TWO
      GO TO 1
C FINISH OFF CALCULATION.
    6 SIMPS1=(ANS+Q/THIRTY)/THREE
      RETURN
C THIS ENTRY USED TO GET AT INTERNAL VARIABLES.
      ENTRY SIMPX1 (TT,NN,QQ)
      T=TT
      NN=N
      QQ=Q
      RETURN
      END
